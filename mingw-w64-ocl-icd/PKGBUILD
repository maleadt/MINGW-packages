# Maintainer: Peter Budai <peterbudai at hotmail.com>

_realname=ocl-icd
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
replaces=("${MINGW_PACKAGE_PREFIX}-${_realname}-git")
pkgver=2.3.2
pkgrel=1
pkgdesc="OpenCL ICD Loader (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clangarm64')
msys2_references=(
  'aur: mingw-w64-ocl-icd'
)
url='https://github.com/OCL-dev/ocl-icd'
license=('spdx:BSD-2-Clause')
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-autotools"
             "${MINGW_PACKAGE_PREFIX}-opencl-headers"
             "${MINGW_PACKAGE_PREFIX}-tools"
             "${MINGW_PACKAGE_PREFIX}-ruby"
             "${MINGW_PACKAGE_PREFIX}-asciidoc"
             "${MINGW_PACKAGE_PREFIX}-dlfcn"
             "xmlto")
source=("https://github.com/OCL-dev/ocl-icd/archive/v${pkgver}/${_realname}-${pkgver}.tar.gz"
        "001-fix-library-linking-and-installation.patch"
        "002-versioned-symbols.patch")
sha256sums=('ec47d7dcd961ea06695b067e8b7edb82e420ddce03e0081a908c62fd0b8535c5'
            '5a17ee05dcb4cd9ceb4c91fcb70ed724c099ea2f170adb8a6579e3e77a2c7471'
            'bb21951caa5dd5c9f21968e07e7988efc76b00a889465dad1560df13b3cb2298')

prepare() {
  cd "${srcdir}"/${_realname}-${pkgver}
  patch -p2 -i "${srcdir}"/001-fix-library-linking-and-installation.patch
  patch -p2 -i "${srcdir}"/002-versioned-symbols.patch

  ./bootstrap
}

build() {
  mkdir -p "${srcdir}"/build-${MSYSTEM} && cd "${srcdir}"/build-${MSYSTEM}

  CONFIGURE_FLAGS=()

  if [[ ${MSYSTEM} == CLANG* ]]; then
    # otherwise configure tries to use gcc
    export CXX=clang++
    export CC=clang

    # `--version-script` is not accepted by the ldd COFF driver
    # https://lists.llvm.org/pipermail/llvm-dev/2018-June/124385.html
    CONFIGURE_FLAGS+=("--disable-map")
  else
    export CXX=g++
    export CC=gcc
  fi

  ../${_realname}-${pkgver}/configure \
    --prefix="${MINGW_PREFIX}" \
    ${CONFIGURE_FLAGS[@]}

  make -j1
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"

  make install DESTDIR="${pkgdir}"
}
